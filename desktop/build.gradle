sourceCompatibility = 17
sourceSets.main.java.srcDirs = [ "src/" ]
sourceSets.main.resources.srcDirs = ["../assets"]

project.ext.mainClassName = "ch.realmtech.core.DesktopLauncher"
project.ext.assetsDir = new File("../assets")

import org.gradle.internal.os.OperatingSystem

task run(dependsOn: classes, type: JavaExec) {
    mainClass = project.mainClassName
    classpath = sourceSets.main.runtimeClasspath
    standardInput = System.in
    workingDir = project.assetsDir
    ignoreExitValue = true

    if (OperatingSystem.current() == OperatingSystem.MAC_OS) {
        // Required to run on macOS
        jvmArgs += "-XstartOnFirstThread"
    }
}

task debug(dependsOn: classes, type: JavaExec) {
    mainClass = project.mainClassName
    classpath = sourceSets.main.runtimeClasspath
    standardInput = System.in
    workingDir = project.assetsDir
    ignoreExitValue = true
    debug = true
}

task dist(type: Jar) {
    duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
    archivesBaseName = 'RealmTech'
    manifest {
        attributes 'Main-Class': project.mainClassName
    }
    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    with jar
}

task javadocJar(type: Jar) {
    archiveClassifier.set('javadoc')
    from project(":server").javadoc
    from project(":core").javadoc
    from javadoc
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from project(':server').sourceSets.main.allSource
    from project(':core').sourceSets.main.allSource
    from sourceSets.main.allSource
}


publishing {
    repositories {
        maven {
            version = "0.2.1-dev"
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/FabienChatton/RealmTech")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME_GITHUB")
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN_GITHUB")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            artifact tasks.dist
            artifact tasks.javadocJar
            artifact tasks.sourcesJar
        }
    }
}

dist.dependsOn classes

eclipse.project.name = appName + "-desktop"
